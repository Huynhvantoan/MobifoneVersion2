apply plugin: 'com.android.application'
android {
    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdkVersion globalConfiguration.getAt("androidCompileSdkVersion")
    buildToolsVersion globalConfiguration.getAt("androidBuildToolsVersion")

    defaultConfig {
        minSdkVersion globalConfiguration.getAt("androidMinSdkVersion")
        targetSdkVersion globalConfiguration.getAt("androidTargetSdkVersion")

        applicationId globalConfiguration.getAt("androidApplicationId")
        versionCode globalConfiguration.getAt("androidVersionCode")
        versionName globalConfiguration.getAt("androidVersionName")
        //multiDexEnabled true
    }
    signingConfigs {
        Key {
            try {
                storeFile file(globalConfiguration.getAt("keystore"))
                storePassword globalConfiguration.getAt("keystorePass")
                keyAlias globalConfiguration.getAt("alias")
                keyPassword globalConfiguration.getAt("aliasPass")
            }
            catch (ignored) {
                throw new InvalidUserDataException("You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.")
            }
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.Key
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
        }
        applicationVariants.all { variant ->
           // configApkFileName(variant, defaultConfig)
        }
    }
    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/rxjava.properties'
    }
    lintOptions {
        quiet true
        abortOnError false
        ignoreWarnings true
        disable 'MissingTranslation'
        disable 'InvalidPackage'            //Some libraries have issues with this.
        disable 'OldTargetApi'
        //Lint gives this warning but SDK 20 would be Android L Beta.
        disable 'IconDensities'             //For testing purpose. This is safe to remove.
        disable 'IconMissingDensityFolder'  //For testing purpose. This is safe to remove.
    }
    dexOptions {
        javaMaxHeapSize "4g" // used by the out-of-process Jack, ignored if running in-process
    }
    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res-main',
                    file('src/main/res-screen')
                            .listFiles()
                            .each { res.srcDirs += it.path }
            ]
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    /*configurations {
        all*.exclude module: 'rxjava'
        all*.exclude module: 'rxandroid'
    }*/
}

dependencies {
    def supportDependencies = rootProject.ext.supportDependencies
    def injectDependencies = rootProject.ext.injectDependencies
    def networkDependencies = rootProject.ext.networkDependencies
    def appDependencies = rootProject.ext.appDependencies

    compile supportDependencies.supportCompat
    compile supportDependencies.supportDesign
    compile supportDependencies.supportRecyclerView
    compile supportDependencies.supportCardView
    compile supportDependencies.customtabs

    //----- Butterknife
    compile supportDependencies.butterKnife
    annotationProcessor supportDependencies.butterknifeCompiler

    //----- Dagger
    compile injectDependencies.dagger
    annotationProcessor injectDependencies.daggerCompiler

    //----- Rx
    compile injectDependencies.rxjava
    compile injectDependencies.rxandroid
    compile injectDependencies.rxbinding

    //----- Network
    compile networkDependencies.retrofit
    compile networkDependencies.okHttp
    compile networkDependencies.okHttpLogging
    compile networkDependencies.retrofitGsonConverter
    compile networkDependencies.retrofitRxJavaAdapter
    compile networkDependencies.glide
    compile networkDependencies.networking
    compile networkDependencies.reactivenetwork

    //App
    compile appDependencies.baseRecyclerView
    compile appDependencies.coordinatortab
    compile appDependencies.bottomTab
    compile appDependencies.fabButton 
    compile(appDependencies.drawer) {
        transitive = true;
    }
    compile 'com.google.firebase:firebase-crash:11.4.2'
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.9.4@aar'
    compile 'com.soundcloud.android.crop:lib_crop:1.0.0'
    compile 'com.darsh.multipleimageselect:multipleimageselect:1.0.4'
    compile 'me.shaohui.advancedluban:library:1.3.2'
    compile 'com.pusher:pusher-java-client:1.4.0'
}

@SuppressWarnings("GroovyAssignabilityCheck")
static def configApkFileName(variant, defaultConfig) {
    variant.outputs.each { output ->
        if (variant.buildType.name == android.buildTypes.debug.name) {
            if (output.zipAlign) {
                def file = output.outputFile
                output.outputFile = new File(file.parentFile, variant.buildType.name + "_v"
                        + defaultConfig.versionName + "_"
                        + defaultConfig.versionCode
                        + ".apk")
            }
        } else if (variant.buildType.name == android.buildTypes.release.name) {
            if (output.zipAlign) {
                def file = output.outputFile
                output.outputFile = new File(file.parentFile, "MobifoneTN" + variant.buildType.name
                        + "_v" + defaultConfig.versionName + "_"
                        + defaultConfig.versionCode
                        + ".apk")
            }
        }
    }
}

apply plugin: 'com.google.gms.google-services'
